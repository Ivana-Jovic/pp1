package rs.ac.bg.etf.pp1;

import java_cup.runtime.*;
import org.apache.log4j.*;

// import java.io.*;
import rs.ac.bg.etf.pp1.ast.*;


parser code {:
	
	boolean errorDetected = false;
	
	Logger log = Logger.getLogger(getClass());
   
   
    // slede redefinisani metodi za prijavu gresaka radi izmene teksta poruke
     
    public void report_fatal_error(String message, Object info) throws java.lang.Exception {
      done_parsing();
      report_error(message, info);
    }
  
    public void syntax_error(Symbol cur_token) {
        report_error("\nSintaksna greska", cur_token);
    }
  
    public void unrecovered_syntax_error(Symbol cur_token) throws java.lang.Exception {
        report_fatal_error("Fatalna greska, parsiranje se ne moze nastaviti", cur_token);
    }

    public void report_error(String message, Object info) {
    	errorDetected = true;
    	StringBuilder msg = new StringBuilder(message); 
    	if (info instanceof Symbol)
            msg.append (" na liniji ").append(((Symbol)info).left);
        log.error(msg.toString());
    }
    
    public void report_info(String message, Object info) {
    	StringBuilder msg = new StringBuilder(message); 
    	if (info instanceof Symbol)
            msg.append (" na liniji ").append(((Symbol)info).left);
        log.info(msg.toString());
    }
    
:}

scan with {:
	Symbol s = this.getScanner().next_token();
	if (s != null && s.value != null) 
		log.info(s.toString() + " " + s.value.toString());
	return s;
:}


terminal PROG, BREAK, CLASS, ENUM, ELSE, CONST, IF, DO, WHILE, NEW, PRINT, READ;
terminal RETURN, VOID, EXTENDS, CONTINUE, THIS, SUPER, GOTO, RECORD;
terminal MINUS, PLUS, MUL, DIV, MOD, EQUAL, NOTEQ, GRT, GRTE, LESS, LESSE;
terminal AND, OR, ASSIGN, PLUSPLUS, MINUSMINUS, COLON, SEMI, COMMA, DOT;

terminal LBRACE, RBRACE, LPAREN, RPAREN, LSQUARE, RSQUARE;
terminal Integer NUMBER ;
terminal String IDENT ;
terminal Character CHAR ;
terminal String BOOL ;

nonterminal Epsilon, ProgList, MethodDeclList, ConstDecl, ConstDeclList;

nonterminal VarDecl, VarDeclList, ClassDecl, VarDeclListList, ConstType;
nonterminal ConstrMeth, MethodDeclListList, RecordDecl, MethodDecl;
nonterminal FormParsList, StatementList, FormPars, Statement, SingleStatement;
nonterminal Statements, DesignatorStatement;
nonterminal ActPars, Condition, CondTerm, CondFact;
nonterminal DesignList, Label, Assignop, Relop, Addop, Mulop;

nonterminal DesStmtASSIGNOP;


nonterminal Variable;
nonterminal RecordDeclName, Unary;
nonterminal rs.etf.pp1.symboltable.concepts.Obj ProgName,Program,MethodTypeName;
nonterminal rs.etf.pp1.symboltable.concepts.Struct Type, MethDeclType, NCBConst ;


nonterminal rs.etf.pp1.symboltable.concepts.Obj Designator, DesignatorIdent;
nonterminal rs.etf.pp1.symboltable.concepts.Struct Expr, Term;
nonterminal rs.etf.pp1.symboltable.concepts.Struct Factorfactor, Factor;

precedence left ELSE;


Program ::= (Program) PROG ProgName ProgList LBRACE MethodDeclList RBRACE ;
ProgName::= (ProgName)IDENT:progName ;

ProgList ::= (ProgListConstDecl) ProgList ConstDecl
			|
			(ProgListVarDecl) ProgList VarDecl
			|
			(ProgListClassDecl) ProgList ClassDecl
			|
			(ProgListRecordDecl) ProgList RecordDecl
			|
			(ProgListNoProgList) Epsilon
			;
MethodDeclList ::= (YMethodDeclList) MethodDeclList MethodDecl
				|		
				(NoMethodDeclList) Epsilon
				;



ConstDecl ::= (ConstDeclNoError) CONST Type ConstType ConstDeclList SEMI

			| (ConstDeclError) CONST error SEMI:s
			{:parser.report_error("*******Const -Izvrsen oporavak do ; u liniji " + sleft,null); :}
			;
ConstDeclList::= (YConstDeclList) ConstDeclList COMMA  ConstType
				|
				(NoConstDeclList) Epsilon
				;
/**OVAKO JE BILO: !!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
ConstType ::= (ConstTypeNumber)IDENT:numIdent ASSIGN NUMBER:numVal
			|
			(ConstTypeChar)IDENT:charIdent ASSIGN CHAR:charVal
			|
			(ConstTypeBool)IDENT:booIdent ASSIGN BOOL:boolVal
		| 
				(ConstTypeError) error:e
			{:parser.report_error("*******Const -Izvrsen oporavak do , u liniji " + eleft,null); :}
			
				
			;
**/
ConstType ::= (ConstTypeNCB)IDENT:ncbIdent ASSIGN NCBConst:ncb
			|
			(ConstTypeError) error:e
			{:parser.report_error("*******Const -Izvrsen oporavak do , u liniji " + eleft,null); :}
			
				
			;
NCBConst::= (NCBConstNum) NUMBER:numVal
			|
			(NCBConstChar) CHAR:charVal
			|
			(NCBConstBool) BOOL:boolVal
			;
/**PROVERI SAD DA LI JE OPORAVAK OD GRESKE OK za ovo gore !!!!!!!!!!!!!!!!!!!!!!!!!!!**/

/**

VarDecl ::= (VarDeclSquare) Type:varType IDENT:varName LSQUARE RSQUARE VarDeclList SEMI
			|
			(VarDeclNoSquare) Type:varType IDENT:varName VarDeclList SEMI
			;
VarDeclList ::= (VarDeclListSquare) VarDeclList COMMA IDENT LSQUARE RSQUARE 
			| 
			(VarDeclListNoSquare) VarDeclList COMMA IDENT 
			|
			(NoVarDeclList) Epsilon
			;

**/


VarDecl ::= (VarDeclSquare) Type:varType VarDeclList SEMI;
VarDeclList ::= (VarDeclListLong) VarDeclList COMMA Variable
			|
			(VarDeclListNoLong)Variable
			;

/**ISTO NOVO**/
Variable::= (VariableNoArray) IDENT:varName
			|
			(VariableArray) IDENT:varName LSQUARE RSQUARE
			;




ClassDecl ::= (ClassDeclType) CLASS IDENT EXTENDS Type LBRACE VarDeclListList ConstrMeth RBRACE
			|
			(ClassDeclNoType) CLASS IDENT LBRACE VarDeclListList ConstrMeth RBRACE
			;
VarDeclListList::=  (YVarDeclListList) VarDeclListList VarDecl
				|
				(NoVarDeclListList) Epsilon
				;
ConstrMeth ::= (ConstrMethNoConstr) LBRACE MethodDeclListList RBRACE 
				|
				(NoConstrMeth) Epsilon
				;
MethodDeclListList ::= (YMethodDeclListList) MethodDeclListList MethodDecl
					|
					(NoMethodDeclListList) Epsilon
					;
	
	
	
	
RecordDecl ::= (RecordDecl) RECORD RecordDeclName LBRACE VarDeclListList RBRACE;
RecordDeclName ::= (RecordDeclName) IDENT:recName;


				





MethodDecl ::= (MethodDecl) MethodTypeName LPAREN FormParsList RPAREN VarDeclListList LBRACE StatementList RBRACE;
MethodTypeName::= (MethodTypeNameType) Type:rett IDENT:methodName
				|
				 (MethodTypeNameVoid) VOID:rett IDENT:methodName
				 ;

/*MethodTypeName::= (MethodTypeName) MethDeclType:rett IDENT:methodName;

MethDeclType::= (MethDeclTypeType) Type
				|
				(MethDeclTypeVoid) VOID
				;
				*/
FormParsList ::= (YFormParsList) FormPars
				| 
				(NoFormParsList) Epsilon
				;
StatementList ::= (YStatementList) StatementList Statement
				| 
				(NoStatementList) Epsilon
				;




FormPars ::= (FormParsSquare)Type IDENT:varN LSQUARE RSQUARE
			|
			(FormParsNoSquare) Type IDENT:varN
			|
			(FormParsSquareList) FormPars COMMA Type IDENT:varN LSQUARE RSQUARE
			|
			(FormParsNoSquareList) FormPars COMMA Type IDENT:varN
			|
			(FormParsError)  error:e 
						{:parser.report_error("*******FormPars -Izvrsen oporavak do ,) u liniji " + eleft,null); :}
			
			;



Type ::= (Type)IDENT:typeName;



Statement ::= (StatementStat) Statements
			|
			(StatementLab)Label COLON SingleStatement
			|
			(StatementSingle)SingleStatement
			;



SingleStatement::= (SingleStatementDesi) DesignatorStatement SEMI
|
(SingleStatementDesStmtASSIGNOP)DesStmtASSIGNOP
					|
					(SingleStatementIfElse) IF LPAREN Condition RPAREN Statement ELSE Statement
					|
					(SingleStatementIf) IF LPAREN Condition RPAREN Statement
					|
					(SingleStatementDo) DO Statement WHILE LPAREN Condition RPAREN SEMI
					|
					(SingleStatementBreak) BREAK SEMI
					|
					(SingleStatementContinue) CONTINUE SEMI
					|
					(SingleStatementRetExpr) RETURN Expr SEMI
					|
					(SingleStatementRet) RETURN SEMI
					|
					(SingleStatementRead) READ LPAREN Designator RPAREN SEMI
					|
					(SingleStatementPrintNum) PRINT LPAREN Expr COMMA NUMBER RPAREN SEMI
					|
					(SingleStatementPrint) PRINT LPAREN Expr RPAREN SEMI
					|
					(SingleStatemenGoto) GOTO Label SEMI
					
					

					;
					




Statements ::= (Statements) LBRACE StatementList RBRACE;

DesignatorStatement ::=
						(DesignatorActpars) Designator LPAREN ActPars RPAREN
						|
						(DesignatorParen) Designator LPAREN RPAREN
						|
						(DesignatorPlus) Designator PLUSPLUS
						|
						(DesignatorMinus) Designator MINUSMINUS
						
						;
						
/*Pre je (DesignatorAssignop) Designator Assignop Expr (bez semi) bilo u DesignatorStatement*/
/*Zbog op od greske je dodato ovo i u SingleStatement */

DesStmtASSIGNOP ::= (DesignatorAssignop) Designator Assignop Expr SEMI
						| 
						(DesignatorStatementError) error SEMI:e
						{:parser.report_error("*******DesignatorStatement -Izvrsen oporavak do ; u liniji " + eleft,null); :}
			;
						
ActPars ::= (ActParsNoList)Expr
			|
			(ActParsList) ActPars COMMA Expr
			;

Condition ::= (ConditionNoList) CondTerm
			|
			(ConditionList) Condition OR CondTerm
			|
			(ConditionError) error:e
						{:parser.report_error("*******Condition -Izvrsen oporavak do ; u liniji " + eleft,null); :}
			
			;
CondTerm ::= (CondTermNoList) CondFact
			|
			(CondTermList) CondTerm AND CondFact
			;
CondFact ::= (CondFactNoList) Expr
			|
			(CondFactList) Expr Relop Expr
			;
/* *(ExprMin)MINUS Term
*/
Expr ::= 
		(ExprTerm) Term
		|
		(ExprAddop) Expr Addop Term
		;
Term ::= (TermFactor)Factor
		|
		(TermMlop)Term Mulop Factor
		;
		/*
		*/
Factor ::= (Factor) Unary Factorfactor;
Unary ::= (UnaryMinus) MINUS
			|
			(UnaryNoMinus) Epsilon
			;
			
Factorfactor ::= (FactorDesParenAct)Designator LPAREN ActPars RPAREN
			|
			(FactorDes) Designator
			|
			(FactorDesPar) Designator LPAREN RPAREN
			|
			(FactorNum) NUMBER:val
			|
			(FactorChar) CHAR:val
			|
			(FactorBool) BOOL:val
			|
			(FactorTypeExpr) NEW Type LSQUARE Expr RSQUARE
			|
			(FactorType) NEW Type
			|
			(FactorParen) LPAREN Expr RPAREN
			; 
Designator ::= (Designator) DesignatorIdent DesignList;
DesignatorIdent::= (DesignatorIdent)IDENT:designName;
DesignList ::= (DesignListDot) DesignList DOT IDENT
			| 
			(DesignListSquare) DesignList LSQUARE Expr:exprr RSQUARE
			|
			(NoDesignList) Epsilon
			;
	
Label ::=(Label) IDENT:lab;
Assignop::= (Assignop) ASSIGN;
Relop ::= (RelopEqual)EQUAL 
		|
		(RelopNotEq) NOTEQ
		|
		(RelopGrt) GRT
		|
		(RelopGrte) GRTE
		|
		(RelopLess) LESS
		|
		(RelopLesse) LESSE
		;
Addop ::= (AddopPlus) PLUS
		|
		(AddopMinus) MINUS
		;
Mulop ::= (MulopMul) MUL 
		|
		(MulopDiv) DIV
		|
		(MulopMod) MOD
		;

Epsilon ::= (Epsilon) /*epsilon */;





















